plugins {
    id 'java-library'
    id 'io.qameta.allure' version '3.0.0' //подключает allure
    id "io.freefair.lombok" version "6.0.0-m2"
}



repositories {
    mavenCentral()
}

//
allure {
    report {
        version.set("2.19.0")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.19.0") //версия интеграции фреймворка и Allure
            }
        }
    }
}

dependencies {
    testImplementation (
            "com.codeborne:selenide:7.10.0",
            "io.qameta.allure:allure-selenide:2.29.1",
            "io.rest-assured:rest-assured:5.5.5",
            "io.qameta.allure:allure-rest-assured:2.29.1",
            "tools.jackson.core:jackson-databind:3.0.0-rc9",
            "org.assertj:assertj-core:3.27.3",
            "io.rest-assured:json-schema-validator:5.3.0",
            "org.junit.jupiter:junit-jupiter:5.9.2",
            "org.slf4j:slf4j-simple:2.0.7"
    )
    implementation("com.github.javafaker:javafaker:1.0.2")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    systemProperties(System.getProperties())
    finalizedBy("allureReport", "copyAllureReport")
}

task api_test(type: Test) {
    useJUnitPlatform {
        includeTags("api")
    }
    finalizedBy("allureReport", "copyAllureReport")
}

task ui_test(type: Test) {
    useJUnitPlatform {
        includeTags("ui")
    }
    finalizedBy("allureReport", "copyAllureReport")
}
//// копирование allure-отчета в дополнительную директорию для telegram
tasks.register("copyAllureReport", Copy) {
    dependsOn("allureReport")
    from("$buildDir/reports/allure-report/allureReport")
    into("$buildDir/allure-report")
}

